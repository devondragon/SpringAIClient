plugins {
    id 'org.springframework.boot' version '3.3.4'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'com.github.ben-manes.versions' version '0.51.0'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'com.vanniktech.maven.publish' version '0.29.0'
}

import com.vanniktech.maven.publish.SonatypeHost
import com.vanniktech.maven.publish.JavaLibrary
import com.vanniktech.maven.publish.JavadocJar

group 'com.digitalsanctuary.springaiclient'
version '1.0.1'
description = 'Simple SpringBoot AI Client Library'

ext {
    springBootVersion = '3.3.4'
    lombokVersion = '1.18.34'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Boot dependencies
    implementation "org.springframework.boot:spring-boot-starter-web:$springBootVersion"

    // Lombok dependencies
    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor:$springBootVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    // Lombok dependencies for test classes
    testCompileOnly "org.projectlombok:lombok:$lombokVersion"
    testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"

    // Testing dependencies
    testImplementation "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
    testImplementation "org.springframework.boot:spring-boot-starter-actuator:$springBootVersion"
}

test {
    useJUnitPlatform()
}

tasks.named('bootJar') {
    enabled = false
}

tasks.named('jar') {
    enabled = true
    archiveBaseName.set('spring-ai-client')
    archiveClassifier.set('')
}

// Maven Central Publishing Tasks
mavenPublishing {
  configure(new JavaLibrary(new JavadocJar.Javadoc(), true))
  publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)
  signAllPublications()
  coordinates("com.digitalsanctuary", "spring-ai-client", project.version)

  pom {
    name = "Spring AI Client"
    description = "Simple SpringBoot AI Client Library."
    inceptionYear = "2024"
    url = "https://github.com/devondragon/SpringAIClient"
    licenses {
      license {
        name = "The Apache License, Version 2.0"
        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
        distribution = "http://www.apache.org/licenses/LICENSE-2.0.txt"
      }
    }
    developers {
      developer {
        id = "devondragon"
        name = "Devon Hillard"
        url = "https://github.com/devondragon/"
      }
    }
    scm {
      url = "https://github.com/devondragon/SpringAIClient"
      connection = "scm:git:git@github.com:devondragon/SpringAIClient.git"
      developerConnection = "scm:git:ssh://git@github.com:devondragon/SpringAIClient.git"
    }
  }
}

tasks.named("publishMavenPublicationToMavenCentralRepository") {
    dependsOn("signMavenPublication")
}
